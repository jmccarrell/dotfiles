#!/usr/bin/env zsh -f
# search across notes and recent work locations with the silver searcher

# this function produces duplicates, as often I will have files in the org agenda file list
#  that reside in one of the directories in search_dirs.
function nag_search_dirs {
    # these directories are not always here in the file system; cf: sift mac.  they are elsewhere.
    #  this is a shortcoming of this simple approach
    local -r -a search_dirs=(
        ${HOME}/jwm/notes
        ${HOME}/jwm/todo
        ${HOME}/pdata/notes
        ${HOME}/pdata/todo
    )
    for d in ${search_dirs[@]}; do
        if [[ -d "$d" ]]; then
            print -l "$d"
        fi
    done

    # search current areas of interest, as represented in my emacs org agena list
    local -r emacs_org_agenda_fname="$HOME/.emacs.d/org-agenda-files-list"
    if [[ -e "$emacs_org_agenda_fname" ]]; then
        while read filename rest; do
            # replace ~ with $HOME
            filename="${filename//\~/$HOME}"
            if [[ -s $filename ]]; then
                # replace files with the directories they reside in
                print -l "$filename:h"
            fi
        done < "$emacs_org_agenda_fname"
    fi
}

# turn on debug tracing just for this named function
# typeset -T -f nag_search_dirs

# de-dup using zsh unique array.  see the manual: zsh builtins: typeset -U
declare -a -U unique_search_dirs=($(nag_search_dirs))
ag $@ $unique_search_dirs
